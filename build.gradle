plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.jetbrains.kotlin.jvm'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()

    maven {
        name = "Maven SciJava"
        url "https://maven.scijava.org/content/groups/public"
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

application {
    mainClass.set('KotlinMainKt')
}

// I'm not sure how much we truly use javafx.
// It gets exercised during measurement export.
// If we can remove this … or use less … we should.
javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation("com.github.ajalt.clikt:clikt:4.4.0")

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // https://mvnrepository.com/artifact/io.github.qupath/qupath-core
    implementation group: 'io.github.qupath', name: 'qupath-core', version: '0.5.1'
    implementation group: 'io.github.qupath', name: 'qupath-core-processing', version: '0.5.1'
    implementation group: 'io.github.qupath', name: 'qupath-gui-fx', version: '0.5.1'

    // We need to force 7.3.1 of formats to work on mac:
    // There's  regression in 7.0.1 and possibly in between.
    implementation group: 'ome', name: 'formats-gpl', version: '7.3.1'

    implementation group: 'io.github.qupath', name: 'qupath-extension-bioformats', version: '0.5.1'

    // Google Cloud APIs
    implementation platform('com.google.cloud:libraries-bom:26.43.0')
    implementation 'com.google.cloud:google-cloud-storage'

    implementation group: 'commons-io', name: 'commons-io', version: '2.16.1'

    // Kotlin stdlib
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

shadowJar {
    zip64 = true
}

test {
    useJUnitPlatform()
}
